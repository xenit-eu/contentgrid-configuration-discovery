allprojects {
    configurations {
        // See https://github.com/gradle/gradle/issues/10861
        // To avoid publishing the internal-platform
        internalPlatform {
            visible = false
            canBeConsumed = false
            canBeResolved = false
        }
        annotationProcessor.extendsFrom(internalPlatform)
        compileClasspath.extendsFrom(internalPlatform)
        runtimeClasspath.extendsFrom(internalPlatform)
        testAnnotationProcessor.extendsFrom(internalPlatform)
        testCompileClasspath.extendsFrom(internalPlatform)
        testRuntimeClasspath.extendsFrom(internalPlatform)
    }

    pluginManager.withPlugin('maven-publish') {
        apply from: "${rootDir}/gradle/publish.gradle"
    }
    pluginManager.withPlugin('java-library') {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
            withSourcesJar()
            withJavadocJar()
        }

        repositories {
            mavenCentral()
        }

        testing {
            suites {
                // Configure the built-in test suite
                test {
                    // Use JUnit Jupiter test framework
                    useJUnitJupiter('6.0.0')
                }
            }
        }
    }
}
